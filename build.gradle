apply plugin:"groovy"
apply plugin:"idea"

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.13'

    compile ('org.apache.hadoop:hadoop-client:2.9.0'){
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
    }
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.25'

    compile group: 'org.apache.camel', name: 'camel-hdfs', version: '2.20.2'
    compile group: 'org.apache.camel', name: 'camel-spring', version: '2.20.2'
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors()
    outputs.upToDateWhen { false }
}

idea {
    module {
        downloadJavadoc = false
        downloadSources = false

        iml.withXml {
            def node = it.asNode()
            def content = node.component.find { it.'@name' == 'NewModuleRootManager' }.content[0]
            content.sourceFolder.each { sourceFolder ->
                if (sourceFolder.@url?.endsWith('/resources')) {
                    sourceFolder.attributes().with {
                        boolean isTestSource = (remove('isTestSource') == 'true')
                        put('type', isTestSource ? 'java-test-resource' : 'java-resource')
                    }
                }
            }
        }
    }
}
